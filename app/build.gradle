plugins {
    alias(libs.plugins.android)
    alias(libs.plugins.kotlin)
    alias(libs.plugins.kapt)
    alias(libs.plugins.ksp)
    alias(libs.plugins.parcelize)
    alias(libs.plugins.apollo)
    alias(libs.plugins.navComponentSafeArgs)
    alias(libs.plugins.junit5)
    id "dagger.hilt.android.plugin"
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.dmko.bulldogvods"
        minSdk 23
        targetSdk 32
        versionCode 2
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            manifestPlaceholders = [sentryEnvironment: "production"]
        }
        debug {
            manifestPlaceholders = [sentryEnvironment: "debug"]
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    buildFeatures {
        viewBinding true
    }
    ksp {
        arg("room.schemaLocation", "$projectDir/schemas".toString())
    }
}

apollo {
    packageName.set("apollo")
}

dependencies {
    implementation(libs.material)

    implementation(libs.androidx.appCompat)
    implementation(libs.androidx.ktx)
    implementation(libs.androidx.constraintLayout)
    implementation(libs.androidx.swipeRefreshLayout)
    implementation(libs.androidx.startup)
    implementation(libs.bundles.androidx.lifecycle)
    implementation(libs.bundles.androidx.datastore)
    implementation(libs.bundles.androidx.paging)

    implementation(libs.bundles.androidx.room)
    ksp(libs.androidx.room.ksp)

    implementation(libs.hilt)
    kapt(libs.hilt.kapt)

    implementation(libs.bundles.coil)
    implementation(libs.animatedWebp)

    implementation(libs.navComponent.fragment)
    implementation(libs.bundles.rxJava3)
    implementation(libs.bundles.apollo)
    implementation(libs.okhttpLoggingInterceptor)
    implementation(libs.liveEvent)
    implementation(libs.exoPlayer)
    implementation(libs.eventBus)

    implementation(libs.timber)
    implementation(libs.sentry)

    testImplementation(libs.bundles.junit5)
    testRuntimeOnly(libs.junit5.engine)

    coreLibraryDesugaring(libs.desugarJdkLibs)
}
